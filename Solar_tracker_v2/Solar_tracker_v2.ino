#define BLYNK_TEMPLATE_ID "TMPL626Up_29-"
#define BLYNK_TEMPLATE_NAME "Solar tracker"
#define BLYNK_AUTH_TOKEN "_sekmk35_3gSPuLM2WmRGgyBVUvOKP-x"

#include <ESP32Servo.h>
#include <DHT.h>
#include <MD_MAX72xx.h>
#include <MD_Parola.h>
#include <SPI.h>
#include <WiFi.h>
#include <BlynkSimpleEsp32.h>

char ssid[] = "MY COFFEE";
// char pass[] = "44hg";
char pass[] = "44hotuong";


// ==== CONFIGURATIONS ==== //
#define DHTPIN 14
#define DHTTYPE DHT11

#define SERVO_LR 18
#define SERVO_UD 19

#define LDR_TOP 34
#define LDR_BOTTOM 35
#define LDR_LEFT 32
#define LDR_RIGHT 33

// ==== LED MATRIX CONFIG ==== //
#define HARDWARE_TYPE MD_MAX72XX::FC16_HW
#define MAX_DEVICES 4
#define DATA_PIN  23
#define CLK_PIN   21
#define CS_PIN    22

#define SOLAR_VOLT_PIN 36  // GPIO36 l√† ch√¢n ADC1_CH0

bool trackingEnabled = true;  // M·∫∑c ƒë·ªãnh l√† b·∫≠t auto-tracking

// Th·ªùi gian g·ª≠i th√¥ng b√°o
unsigned long lastEventTime = 0;
const unsigned long eventCooldown = 30000; // 30 gi√¢y

DHT dht(DHTPIN, DHTTYPE);
MD_Parola display = MD_Parola(HARDWARE_TYPE, DATA_PIN, CLK_PIN, CS_PIN, MAX_DEVICES);

Servo servoLR;
Servo servoUD;

int posLR = 90;  // b·∫Øt ƒë·∫ßu ·ªü gi·ªØa kho·∫£ng 0-180
int posUD = 90;

int valTop, valBottom, valLeft, valRight;
const int threshold = 250;
const int stepSize = 1; // step ƒë·ªô ƒë·ªÉ servo di chuy·ªÉn m∆∞·ª£t h∆°n

static unsigned long lastDHT = 0;
const unsigned long dhtInterval = 2000;

bool newDisplayMsg = false;
char currentMsg[128];

// Ki·ªÉm so√°t th·ªùi gian ƒë·ªçc LDR v√† c·∫≠p nh·∫≠t servo (v√≠ d·ª• 50ms)
static unsigned long lastLDR = 0;
const unsigned long ldrInterval = 100;
static unsigned long lastsolarVol = 0;
static unsigned long log_LDR = 0;

void read_LDR_and_updateServo() {
  valTop = analogRead(LDR_TOP);
  valBottom = analogRead(LDR_BOTTOM);
  valLeft = analogRead(LDR_LEFT);
  valRight = analogRead(LDR_RIGHT);

  int vertDiff = valTop - valBottom;
  int horizDiff = valLeft - valRight;

  unsigned long now = millis();
  if (now - log_LDR > 2000) {
    log_LDR = now;
    Serial.print("Top: "); Serial.print(valTop);
    Serial.print(" | Bottom: "); Serial.print(valBottom);
    Serial.print(" | Left: "); Serial.print(valLeft);
    Serial.print(" | Right: "); Serial.print(valRight);

    Serial.print(" | vertDiff: "); Serial.print(vertDiff);
    Serial.print(" | horizDiff: "); Serial.print(horizDiff);

    Serial.print(" | posUD: "); Serial.print(posUD);
    Serial.print(" | posLR: "); Serial.println(posLR);
  }

  // C·∫≠p nh·∫≠t posUD n·∫øu kh√°c bi·ªát ƒë·ªß l·ªõn
  if (abs(vertDiff) > threshold) {
    if (vertDiff > 0) posUD += stepSize;
    else posUD -= stepSize;
    posUD = constrain(posUD, 20, 160);
    servoUD.write(posUD);
  }

  // C·∫≠p nh·∫≠t posLR n·∫øu kh√°c bi·ªát ƒë·ªß l·ªõn
  if (abs(horizDiff) > threshold + 150) {
    if (horizDiff > 0) posLR += stepSize;
    else posLR -= stepSize;
    posLR = constrain(posLR, 20, 160);
    servoLR.write(posLR);
  }
}

float readSolarVoltage() {
  int adcVal = analogRead(SOLAR_VOLT_PIN);
  float vMeasured = (adcVal / 4095.0) * 3.3;  // ƒêi·ªán √°p t·∫°i ƒëi·ªÉm chia (V_out)

  // M·∫°ch ph√¢n √°p: R1 = 100Œ© (tr√™n), R2 = 30kŒ© (d∆∞·ªõi)
  // Vout = Vin * (R2 / (R1 + R2)) ‚Üí Vin = Vout * ((R1 + R2) / R2)
  float vin = vMeasured * ((100.0 + 33000.0) / 33000.0);

  return vin;
}

BLYNK_WRITE(V8) {
  int value = param.asInt();  // 1: c·ªë ƒë·ªãnh, 0: auto tracking

  if (value == 1) {
    trackingEnabled = false;
    Serial.println("üîí Tracking OFF - Servo gi·ªØ nguy√™n v·ªã tr√≠");
  } else {
    trackingEnabled = true;
    Serial.println("‚úÖ Tracking ON - Servo b·∫Øt ƒë·∫ßu d√≤ s√°ng");
  }
}


void setup() {
  Serial.begin(115200);
  delay(1000);
  Serial.println("=== ESP32 Solar Tracker ===");

  Blynk.begin(BLYNK_AUTH_TOKEN, ssid, pass);
  dht.begin();

  // G√°n tr∆∞·ªõc gi√° tr·ªã g√≥c kh·ªüi t·∫°o
  posLR = 90;
  posUD = 0;

  // Kh·ªüi t·∫°o t·∫ßn s·ªë PWM v√† attach sau khi c√≥ g√≥c ƒë√∫ng
  servoLR.setPeriodHertz(50);
  servoUD.setPeriodHertz(50);
  servoLR.attach(SERVO_LR, 500, 2400);
  servoUD.attach(SERVO_UD, 500, 2400);

  // Sau khi attach th√¨ m·ªõi write
  servoLR.write(posLR);
  servoUD.write(posUD);

  analogReadResolution(12);  // ESP32 m·∫∑c ƒë·ªãnh 12-bit (0 - 4095)

  display.begin();
  display.setIntensity(5);
  display.displayClear();
}


String lastWarningMsg = "";  // L∆∞u c·∫£nh b√°o tr∆∞·ªõc ƒë√≥

void sendToBlynk(float temp, float humi, float solarVolt) {
    Blynk.virtualWrite(V0, temp);
    Blynk.virtualWrite(V1, humi);
    Blynk.virtualWrite(V6, solarVolt);

    Serial.printf("Sending to Blynk - Temp: %.1f, Humi: %.1f, SolarVolt: %.2f\n", temp, humi, solarVolt);

    String warningMsg;
    if (solarVolt > 8.0) {
        warningMsg = "‚ö†Ô∏è C·∫£nh b√°o: C√¥ng su·∫•t v∆∞·ª£t ng∆∞·ª°ng!";
        if (lastWarningMsg != warningMsg) {
            Blynk.virtualWrite(V7, warningMsg);
            lastWarningMsg = warningMsg;
        }
        if (millis() - lastEventTime > eventCooldown) {
            Serial.println("Logging event: cng_sut_vt_ngng");
            Blynk.logEvent("cng_sut_vt_ngng", warningMsg);
            lastEventTime = millis();
        }
    } else if (solarVolt < 1.0) {
        warningMsg = "‚ö†Ô∏è C·∫£nh b√°o: C√¥ng su·∫•t qu√° th·∫•p!";
        Serial.println("Condition met: solarVolt < 1.0");
        if (lastWarningMsg != warningMsg) {
            Blynk.virtualWrite(V7, warningMsg);
            lastWarningMsg = warningMsg;
        }
        if (millis() - lastEventTime > eventCooldown) {
            Serial.println("Logging event: cng_sut_qu_thp");
            Blynk.logEvent("cng_sut_qu_thp", warningMsg);
            lastEventTime = millis();
        } else {
            Serial.printf("Event cooldown active: %lu ms remaining\n", eventCooldown - (millis() - lastEventTime));
        }
    } else {
        warningMsg = "‚úÖ An to√†n: C√¥ng su·∫•t ·ªïn ƒë·ªãnh.";
        if (lastWarningMsg != warningMsg) {
            Blynk.virtualWrite(V7, warningMsg);
            lastWarningMsg = warningMsg;
        }
    }
}

void loop() {
  unsigned long now = millis();

  // ƒê·ªçc v√† x·ª≠ l√Ω LDR v·ªõi t·∫ßn su·∫•t 50ms
  if (now - lastLDR >= ldrInterval) {
    lastLDR = now;
    if (trackingEnabled) {
      read_LDR_and_updateServo();
    }
  }

  if (now - lastsolarVol >= dhtInterval) {
    lastsolarVol = now;
    float solarV = readSolarVoltage();
    Serial.printf("Solar Panel Voltage: %.2f V\n", solarV);
  }

  static unsigned long lastLDRSend = 0;
  const unsigned long ldrSendInterval = 2000;

  if (now - lastLDRSend >= ldrSendInterval) {
    lastLDRSend = now;
    Blynk.virtualWrite(V2, valTop);
    Blynk.virtualWrite(V3, valBottom);
    Blynk.virtualWrite(V4, valLeft);
    Blynk.virtualWrite(V5, valRight);
  }

  // ƒê·ªçc DHT11 v·ªõi t·∫ßn su·∫•t 2000ms
  if (now - lastDHT >= dhtInterval) {
    lastDHT = now;

    float t = dht.readTemperature();
    float h = dht.readHumidity();
    float solarV = readSolarVoltage();

    if (!isnan(t) && !isnan(h)) {
      Serial.printf("Temp: %.1f¬∞C | Humi: %.1f%% | Solar: %.2fV\n", t, h, solarV);
      snprintf(currentMsg, sizeof(currentMsg), "T:%.1fC H:%.1f%%", t, h);
      newDisplayMsg = true;

      sendToBlynk(t, h, solarV);  // G·ª≠i d·ªØ li·ªáu l√™n Blynk
    } else {
      Serial.println("L·ªói ƒë·ªçc c·∫£m bi·∫øn DHT!");
    }
  }

  Blynk.run();  // Lu√¥n g·ªçi Blynk.run trong loop

  // C·∫≠p nh·∫≠t hi·ªÉn th·ªã LED matrix n·∫øu c√≥ tin nh·∫Øn m·ªõi
  if (newDisplayMsg) {
    display.displayZoneText(0, currentMsg, PA_LEFT, 20, 15000, PA_SCROLL_LEFT, PA_SCROLL_LEFT);
    display.displayReset(0);
    newDisplayMsg = false;
  }

  display.displayAnimate();
}
